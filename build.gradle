plugins {
    id "com.gradle.plugin-publish" version "0.11.0"
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'jacoco'
    id "org.sonarqube" version "2.8"
}

group 'org.padler.gradle.minify'
version '1.4.0-SNAPSHOT'

sourceCompatibility = 1.8

ext['gradle.publish.key'] = System.getenv('GRADLE_PUBLISH_KEY')
ext['gradle.publish.secret'] = System.getenv('GRADLE_PUBLISH_SECRET')

test.finalizedBy jacocoTestReport

jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.yahoo.platform.yui:yuicompressor:2.4.8'

    // https://mvnrepository.com/artifact/com.google.auto.value/auto-value-annotations
    implementation 'com.google.auto.value:auto-value-annotations:1.7'

    // https://mvnrepository.com/artifact/com.google.javascript/closure-compiler
    implementation 'com.google.javascript:closure-compiler:v20200315'

    // https://mvnrepository.com/artifact/com.google.closure-stylesheets/closure-stylesheets
    implementation 'com.google.closure-stylesheets:closure-stylesheets:1.5.0'

    testImplementation 'junit:junit:4.12'
}

gradlePlugin {
    plugins {
        minifyPlugin {
            id = 'org.padler.gradle.minify'
            implementationClass = 'org.padler.gradle.minify.MinifyPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/616slayer616/gradle-minify-plugin'
    vcsUrl = 'https://github.com/616slayer616/gradle-minify-plugin'
    description = 'A simple gradle plugin to minify CSS and JavaScript files'
    tags = ['css', 'javascript', 'js', 'minify', 'minification']

    plugins {
        minifyPlugin {
            displayName = 'Gradle Minify Plugin'
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectName", "Gradle Minify Plugin"
        property "sonar.projectKey", "616slayer616_gradle-minify-plugin"
        property "sonar.coverage.exclusions", '**/*Extension*'
    }
}
